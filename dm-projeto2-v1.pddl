(define (domain recife-c)
 (:requirements :strips)
 (:predicates
  (eh-ciclista ?quem)
  (eh-estacao ?qual)
  (eh-bicicleta ?qual)
  (eh-ponto-visita ?qual)
  (eh-adjacente ?est1 ?est2)
  (eh-ponto-visita-prox ?qualponto ?qualest)
  (ciclista-em-estacao ?quem ?onde)
  (ciclista-em-ponto-de-visita ?quem ?onde)
  (ponto-visitado ?quem ?qual-ponto)
  (ciclista-com-bicicleta ?quem)
  (bicicleta-em-estacao ?qual ?onde)
  (bicicleta-com-ciclista ?qual ?comquem)
  (tem-bicicleta ?que-est)
  (pode-pegar-bicicleta ?quem)
  (meia-hora ?de-que-est ?para-qual-est)
  (pronto-pra-pedalar ?quem)
  (pronto-pra-visitar ?quem)
  (eh-ponto-a-ser-visitado ?por-quem ?qual-ponto)
  (eh-ponto-prioridade ?de-quem ?qual-ponto)
  (definiu-ponto-prioridade ?quem)
  (tem-locais-a-visitar ?quem)
  (eh-ponto-final ?de-quem ?qual-ponto)
  (tem-que-esperar-amigo ?quem ?aonde)
  (amigo-a-esperar ?quem ?a-quem)
  (pode-visitar ?quem)
  (esperando-amigo ?quem)
  (percurso-cumprido ?quem)
  (5min-esperado ?quem))

 (:action pega-bicicleta
   :parameters (?quem ?qual-est ?qual-bike)
   :precondition (and (eh-ciclista ?quem)
          (eh-bicicleta ?qual-bike)
          (eh-estacao ?qual-est)
          (tem-bicicleta ?qual-est)
          (ciclista-em-estacao ?quem ?qual-est)
          (not (ciclista-com-bicicleta ?quem))
          (bicicleta-em-estacao ?qual-bike ?qual-est)
          (pode-pegar-bicicleta ?quem))
   :effect (and (not (bicicleta-em-estacao ?qual-bike ?qual-est))
   (bicicleta-com-ciclista ?qual-bike ?quem)
   (ciclista-com-bicicleta ?quem)
   (pronto-pra-pedalar ?quem)))

 (:action entrega-bicicleta
   :parameters (?quem ?qual-bike ?qual-est)
   :precondition (and (eh-ciclista ?quem)
          (eh-bicicleta ?qual-bike)
          (eh-estacao ?qual-est)
          (ciclista-em-estacao ?quem ?qual-est)
          (bicicleta-com-ciclista ?qual-bike ?quem)
          (ciclista-com-bicicleta ?quem)
          (not (bicicleta-em-estacao ?qual-bike ?qual-est))
          (not (pronto-pra-pedalar ?quem)))
   :effect (and (bicicleta-em-estacao ?qual-bike ?qual-est)
   (not (bicicleta-com-ciclista ?qual-bike ?quem))
   (not (ciclista-com-bicicleta ?quem))
   (not (pode-pegar-bicicleta ?quem))))

 (:action espera-5min
   :parameters (?quem ?onde ?ponto-destino)
   :precondition (and (eh-ciclista ?quem)
          (eh-estacao ?onde)
          (eh-ponto-visita ?ponto-destino)
          (eh-ponto-visita-prox ?ponto-destino ?onde)
          (not (eh-ponto-prioridade ?quem ?ponto-destino))
          (ciclista-em-estacao ?quem ?onde)
          (tem-bicicleta ?onde)
          (not (ciclista-com-bicicleta ?quem))
          (not (pronto-pra-pedalar ?quem))
          (not (5min-esperado ?quem)))
   :effect (5min-esperado ?quem))

  (:action verifica-que-ha-bicicletas
   :parameters (?quem ?onde ?bicicleta)
   :precondition (and (eh-ciclista ?quem)
          (eh-estacao ?onde)
          (eh-bicicleta ?bicicleta)
          (ciclista-em-estacao ?quem ?onde)
          (not (ciclista-com-bicicleta ?quem))
          (not (pronto-pra-pedalar ?quem))
          (not (pode-pegar-bicicleta ?quem))
          (5min-esperado ?quem)
          (bicicleta-em-estacao ?bicicleta ?onde))
   :effect (and (tem-bicicleta ?onde)
          (pode-pegar-bicicleta ?quem)))

 (:action caminha
   :parameters (?quem ?de-que-est ?para-qual-ponto)
   :precondition (and (eh-ciclista ?quem)
          (eh-estacao ?de-que-est)
          (eh-ponto-visita ?para-qual-ponto)
          (ciclista-em-estacao ?quem ?de-que-est)
          (eh-ponto-visita-prox ?para-qual-ponto ?de-que-est)
          (not (ciclista-com-bicicleta ?quem))
          (not (ciclista-em-ponto-de-visita ?quem ?para-qual-ponto))
          (not (ponto-visitado ?quem ?para-qual-ponto))
          (eh-ponto-a-ser-visitado ?quem ?para-qual-ponto)
          (eh-ponto-prioridade ?quem ?para-qual-ponto))
   :effect (and (not (ciclista-em-estacao ?quem ?de-que-est))
   (ciclista-em-ponto-de-visita ?quem ?para-qual-ponto)))

  (:action esperar-amigo
   :parameters (?quem ?em-qual-ponto)
   :precondition (and (eh-ciclista ?quem)
          (eh-ponto-visita ?em-qual-ponto)
          (ciclista-em-ponto-de-visita ?quem ?em-qual-ponto)
          (not (ponto-visitado ?quem ?em-qual-ponto))
          (tem-que-esperar-amigo ?quem ?em-qual-ponto))
   :effect (and (not (pode-visitar ?quem)) (esperando-amigo ?quem)))
  
  (:action encontrar-amigo
   :parameters (?quem ?a-quem ?em-qual-ponto)
   :precondition (and (eh-ciclista ?quem)
          (eh-ciclista ?a-quem)
          (eh-ponto-visita ?em-qual-ponto)
          (ciclista-em-ponto-de-visita ?quem ?em-qual-ponto)
          (ciclista-em-ponto-de-visita ?a-quem ?em-qual-ponto)
          (amigo-a-esperar ?quem ?a-quem)
          (not (pode-visitar ?quem))
          (esperando-amigo ?quem)
          (not (ponto-visitado ?quem ?em-qual-ponto)))
   :effect (and (pode-visitar ?quem) (pode-visitar ?a-quem) 
   (not (tem-que-esperar-amigo ?quem ?em-qual-ponto))
   (not (tem-que-esperar-amigo ?a-quem ?em-qual-ponto))))

 (:action visitar-ponto
   :parameters (?quem ?qual-ponto)
   :precondition (and (eh-ciclista ?quem)
          (eh-ponto-visita ?qual-ponto)
          (ciclista-em-ponto-de-visita ?quem ?qual-ponto)
          (pode-visitar ?quem)
          (not (tem-que-esperar-amigo ?quem ?qual-ponto))
          (not (ponto-visitado ?quem ?qual-ponto)))
   :effect (and (ponto-visitado ?quem ?qual-ponto)
           (not (eh-ponto-a-ser-visitado ?quem ?qual-ponto))))

  (:action pensar-no-proximo-ponto
   :parameters (?quem ?em-qual-ponto ?para-qual-ponto)
   :precondition (and (eh-ciclista ?quem)
          (eh-ponto-visita ?em-qual-ponto)
          (eh-ponto-visita ?para-qual-ponto)
          (ciclista-em-ponto-de-visita ?quem ?em-qual-ponto)
          (ponto-visitado ?quem ?em-qual-ponto)
          (not (eh-ponto-a-ser-visitado ?quem ?em-qual-ponto))
          (not (eh-ponto-final ?quem ?em-qual-ponto))
          (eh-ponto-a-ser-visitado ?quem ?para-qual-ponto))
   :effect (and (eh-ponto-prioridade ?quem ?para-qual-ponto)
   (not(eh-ponto-prioridade ?quem ?em-qual-ponto))
   (definiu-ponto-prioridade ?quem)))

  (:action ver-que-e-ponto-final
   :parameters (?quem ?em-qual-ponto)
   :precondition (and (eh-ciclista ?quem)
          (eh-ponto-visita ?em-qual-ponto)
          (ciclista-em-ponto-de-visita ?quem ?em-qual-ponto)
          (ponto-visitado ?quem ?em-qual-ponto)
          (not (eh-ponto-a-ser-visitado ?quem ?em-qual-ponto))
          (eh-ponto-final ?quem ?em-qual-ponto))
   :effect (not (tem-locais-a-visitar ?quem)))

  (:action voltar-para-estacao
   :parameters (?quem ?de-qual-ponto ?para-qual-est)
   :precondition (and (eh-ciclista ?quem)
          (eh-estacao ?para-qual-est)
          (eh-ponto-visita ?de-qual-ponto)
          (ciclista-em-ponto-de-visita ?quem ?de-qual-ponto)
          (not (ciclista-em-estacao ?quem ?para-qual-est))
          (ponto-visitado ?quem ?de-qual-ponto)
          (tem-locais-a-visitar ?quem)
          (eh-ponto-visita-prox ?de-qual-ponto ?para-qual-est)
          (definiu-ponto-prioridade ?quem))
   :effect (and (ciclista-em-estacao ?quem ?para-qual-est)
   (not (ciclista-em-ponto-de-visita ?quem ?de-qual-ponto))
   (pode-pegar-bicicleta ?quem)))

  (:action finalizar-passeio
   :parameters (?quem ?em-qual-ponto)
   :precondition (and (eh-ciclista ?quem)
          (eh-ponto-visita ?em-qual-ponto)
          (ciclista-em-ponto-de-visita ?quem ?em-qual-ponto)
          (ponto-visitado ?quem ?em-qual-ponto)
          (eh-ponto-final ?quem ?em-qual-ponto)
          (not (tem-locais-a-visitar ?quem)))
   :effect (percurso-cumprido ?quem))

 (:action pedalar
   :parameters (?quem ?de-que-est ?para-qual-est)
   :precondition (and (eh-ciclista ?quem)
          (eh-estacao ?de-que-est)
          (eh-estacao ?para-qual-est)
          (eh-adjacente ?de-que-est ?para-qual-est)
          (ciclista-em-estacao ?quem ?de-que-est)
          (ciclista-com-bicicleta ?quem)
          (pronto-pra-pedalar ?quem)
          (not (meia-hora ?de-que-est ?para-qual-est))
          (not (ciclista-em-estacao ?quem ?para-qual-est)))
   :effect (and (not (ciclista-em-estacao ?quem ?de-que-est))
   (ciclista-em-estacao ?quem ?para-qual-est)
   (meia-hora ?de-que-est ?para-qual-est)
   (not (pronto-pra-pedalar ?quem))))
)